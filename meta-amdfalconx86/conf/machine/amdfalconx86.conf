#@TYPE: Machine
#@NAME: amdfalconx86

#@DESCRIPTION: Machine configuration for amdfalconx86 systems


PREFERRED_PROVIDER_virtual/kernel ?= "${@bb.utils.contains('RT_KERNEL_AMDFALCONX86', 'yes', "linux-yocto-rt", "linux-yocto", d)}"
PREFERRED_PROVIDER_jpeg ?= "jpeg"
PREFERRED_PROVIDER_jpeg-native ?= "jpeg-native"
PREFERRED_PROVIDER_llvm ?= "llvm3.7.0"
PREFERRED_VERSION_linux-yocto ?= "3.14%"
PREFERRED_VERSION_linux-yocto-rt ?= "3.14%"
PREFERRED_VERSION_mesa = "10.7.0+git%"
PREFERRED_VERSION_gstreamer1.0-omx = "git+git%"
PREFERRED_VERSION_libav = "9.18"
PREFERRED_VERSION_libdrm = "2.4.63"
PREFERRED_VERSION_glamor-egl = "0.6.0+git%"
PREFERRED_VERSION_xf86-video-amdgpu = "0.0.01+git%"

require conf/machine/include/tune-amdfalconx86.inc

MULTILIBS ?= ""
require conf/multilib.conf

MACHINE_FEATURES += "wifi efi pcbios"

# Disable GPU if RT kernel is in use
XSERVER_X86_AMDGPU = "xf86-video-amdgpu \
           ${@base_contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeonsi', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMDFALCONX86', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_AMDGPU}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
           "

MACHINE_EXTRA_RRECOMMENDS += "kernel-modules amdgpu-firmware alsa-utils"
MACHINE_EXTRA_RRECOMMENDS += "vdpauinfo gstreamer1.0-omx"
MACHINE_EXTRA_RRECOMMENDS += "grub parted util-linux-blkid"
MACHINE_EXTRA_RRECOMMENDS += "spi-test rtc-test watchdog-test smbus-test gpio-test"
MACHINE_EXTRA_RRECOMMENDS += "kernel-module-amd-spi kernel-module-amd-wdt"
MACHINE_EXTRA_RRECOMMENDS += "amd-wdt-load"
MACHINE_EXTRA_RRECOMMENDS += "${@bb.utils.contains_any("DISTRO", "mel mel-lite", "firmware-wireless", "", d)}"

KERNEL_MODULE_AUTOLOAD += "snd-soc-acp-pcm snd-soc-acp-rt286-mach"

EXTRA_OECONF_append_pn-matchbox-panel-2 = " --with-battery=acpi"

# Setup a gettys on all serial ports
SERIAL_CONSOLES = "115200;ttyS4 115200;ttyS5 115200;ttyUSB0"

# Enable the kernel console on ttyS4/COM0
APPEND += "console=ttyS4,115200n8"

# Make sure the kernel messages go to the VGA console
APPEND += "console=tty0"

RELEASE_IMAGE ?= "core-image-sato"

APPEND += "amdgpu.dpm=1 amdgpu.dal=0"

# Make sure the VESA VGA driver goes up on boot, we provide a generic mode here
# The AMD GPU driver will take over once it is up
APPEND += "vga=802"

MACHINEOVERRIDES =. "amd:amdgpu:"
