#@TYPE: Machine
#@NAME: baldeagle

#@DESCRIPTION: Machine configuration for baldeagle systems


PREFERRED_PROVIDER_virtual/kernel ?= "linux-amd"
PREFERRED_PROVIDER_jpeg ?= "jpeg"
PREFERRED_PROVIDER_jpeg-native ?= "jpeg-native"
PREFERRED_VERSION_linux-amd = "3.12%"
PREFERRED_VERSION_gstreamer1.0-omx = "git+git%"
PREFERRED_VERSION_llvm = "3.4"
PREFERRED_VERSION_libav = "9.18"

require conf/machine/include/tune-baldeagle.inc

MULTILIBS ?= ""
require conf/multilib.conf

MACHINE_FEATURES += "wifi efi pcbios"

# Disable GPU if RT kernel is in use
XSERVER_X86_RADEON = "xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeon', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_RADEON}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
            "

MACHINE_EXTRA_RRECOMMENDS += "kernel-modules radeon-firmware alsa-utils"
MACHINE_EXTRA_RRECOMMENDS += "vdpauinfo grub parted util-linux-blkid"
MACHINE_EXTRA_RRECOMMENDS += "rtc-test watchdog-test spi-test smbus-test gpio-test"
MACHINE_EXTRA_RRECOMMENDS += "amd-gpio amd-spi amd-wdt amd-gpio-load amd-wdt-load"
MACHINE_EXTRA_RRECOMMENDS += "${@bb.utils.contains_any("DISTRO", "mel mel-lite", "firmware-wireless", "", d)}"

EXTRA_OECONF_append_pn-matchbox-panel-2 = " --with-battery=acpi"

# Setup a gettys on all serial ports
SERIAL_CONSOLES = "115200;ttyUSB0 115200;ttyS1 115200;ttyS0"

# Make sure the kernel messages go to the VGA console and to the serial ports.
APPEND += "console=ttyS0,115200 console=tty0"

# Make sure the VESA VGA driver goes up on boot, we provide a generic mode here
APPEND += "vga=802"

RELEASE_IMAGE ?= "core-image-sato"

APPEND += "radeon.dpm=1"

MACHINEOVERRIDES =. "amd:amdx86:radeon:"
