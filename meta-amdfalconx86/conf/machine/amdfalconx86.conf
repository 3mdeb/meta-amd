#@TYPE: Machine
#@NAME: amdfalconx86

#@DESCRIPTION: Machine configuration for amdfalconx86 systems

PREFERRED_PROVIDER_virtual/kernel ?= "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "linux-yocto-rt", "linux-yocto", d)}"
PREFERRED_VERSION_linux-yocto-rt ?= "4.1%"
PREFERRED_VERSION_libdrm = "2.4.66"

require conf/machine/include/tune-amdfalconx86.inc

include conf/machine/include/amd-common-configurations.inc

# Disable GPU if RT kernel is in use
XSERVER_X86_AMDGPU = "xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeonsi', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_AMDGPU}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
           "

MACHINE_EXTRA_RRECOMMENDS += "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "", "amdgpu-firmware amd-acp-rt286-load", d)}"

KERNEL_MODULE_AUTOLOAD += "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "", "amd_gnb_bus snd-soc-acp-pcm snd-soc-acp-rt286-mach", d)}"

# Setup a gettys on all serial ports
SERIAL_CONSOLES = "115200;ttyS0 115200;ttyUSB0"

# These are only needed for Bettongs where console doesn't
# work on ttyS0/ttyS1 so we hope to at least get a getty running
# on ttyS4/ttyS5
SERIAL_CONSOLES += "115200;ttyS4 115200;ttyS5"

# Enable the kernel console on ttyS0/COM0
APPEND += "console=ttyS0,115200n8"

# Enable powerplay
APPEND += "amdgpu.powerplay=1"

MACHINEOVERRIDES =. "amd:amdx86:amdgpu:"
