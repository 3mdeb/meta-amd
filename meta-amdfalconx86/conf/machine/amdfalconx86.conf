#@TYPE: Machine
#@NAME: amdfalconx86

#@DESCRIPTION: Machine configuration for amdfalconx86 systems

POKY_DEFAULT_DISTRO_FEATURES_remove = "wayland"

PREFERRED_PROVIDER_virtual/kernel ?= "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "linux-yocto-rt", "linux-yocto", d)}"
PREFERRED_PROVIDER_jpeg ?= "jpeg"
PREFERRED_PROVIDER_jpeg-native ?= "jpeg-native"
PREFERRED_PROVIDER_llvm ?= "llvm3.7.1"
PREFERRED_VERSION_linux-yocto ?= "4.1%"
PREFERRED_VERSION_linux-yocto-rt ?= "4.1%"
PREFERRED_VERSION_mesa = "11.0.8+git%"
PREFERRED_VERSION_gstreamer1.0-omx = "git+git%"
PREFERRED_VERSION_libav = "9.18"
PREFERRED_VERSION_libdrm = "2.4.66"

require conf/machine/include/tune-amdfalconx86.inc

MULTILIBS ?= ""
require conf/multilib.conf

MACHINE_FEATURES += "wifi efi pcbios alsa"

# QEMU does not support some of the enhanced instructions available
# on the AMD chips which causes some build time failures.
MACHINE_FEATURES_remove = "qemu-usermode"

# Disable GPU if RT kernel is in use
XSERVER_X86_AMDGPU = "xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeonsi', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_AMDGPU}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
           "

MACHINE_EXTRA_RRECOMMENDS += "kernel-modules amdgpu-firmware alsa-utils"
MACHINE_EXTRA_RRECOMMENDS += "vdpauinfo gstreamer1.0-omx"
MACHINE_EXTRA_RRECOMMENDS += "grub parted util-linux-blkid"
MACHINE_EXTRA_RRECOMMENDS += "rtc-test watchdog-test smbus-test gpio-test spi-test"
MACHINE_EXTRA_RRECOMMENDS += "amd-wdt amd-wdt-load amd-spi amd-acp-rt286-load"
MACHINE_EXTRA_RRECOMMENDS += "${@bb.utils.contains_any("DISTRO", "mel mel-lite", "firmware-wireless", "", d)}"

KERNEL_MODULE_AUTOLOAD += "amd_gnb_bus snd-soc-acp-pcm snd-soc-acp-rt286-mach"

EXTRA_OECONF_append_pn-matchbox-panel-2 = " --with-battery=acpi"

# Setup a gettys on all serial ports
SERIAL_CONSOLES = "115200;ttyS0 115200;ttyUSB0"

# These are only needed for Bettongs where console doesn't
# work on ttyS0/ttyS1 so we hope to at least get a getty running
# on ttyS4/ttyS5
SERIAL_CONSOLES += "115200;ttyS4 115200;ttyS5"

# Enable the kernel console on ttyS0/COM0
APPEND += "console=ttyS0,115200n8"

# Make sure the kernel messages go to the VGA console
APPEND += "console=tty0"

RELEASE_IMAGE ?= "core-image-sato"

# Make sure the VESA VGA driver goes up on boot, we provide a generic mode here
# The AMD GPU driver will take over once it is up
APPEND += "vga=802"

# Enable powerplay
APPEND += "amdgpu.powerplay=1"

MACHINEOVERRIDES =. "amd:amdx86:amdgpu:"
