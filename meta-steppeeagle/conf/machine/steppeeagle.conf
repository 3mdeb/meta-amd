#@TYPE: Machine
#@NAME: steppeeagle

#@DESCRIPTION: Machine configuration for steppeeagle systems

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

require conf/machine/include/tune-steppeeagle.inc

include conf/machine/include/amd-common-configurations.inc

# Disable GPU if RT kernel is in use
XSERVER_X86_RADEON = "xf86-video-amd \
           ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-radeon', '', d)} \
           "
XSERVER_X86_NOGPU = "${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-swrast', '', d)}"
XSERVER_X86_GPU = "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "${XSERVER_X86_NOGPU}", "${XSERVER_X86_RADEON}", d)}"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_GPU} \
           "

MACHINE_EXTRA_RRECOMMENDS += "radeon-firmware"
MACHINE_EXTRA_RRECOMMENDS += "amd-gpio"

# Setup a gettys on all serial ports
SERIAL_CONSOLES = "115200;ttyUSB0"

# Make sure the kernel messages go to the VGA console and to the serial ports.
KERNEL_SERIAL_CONSOLE = "console=ttyUSB0,115200n8"

APPEND += "radeon.dpm=1"

MACHINEOVERRIDES =. "amd:amdx86:radeon:"

# Metadata used by CodeBench for the ADE
ADE_CB_CPU = "general.cpu.puma"
