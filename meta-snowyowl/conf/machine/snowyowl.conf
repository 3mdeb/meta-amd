#@TYPE: Machine
#@NAME: snowyowl

#@DESCRIPTION: Machine configuration for snowyowl systems

PREFERRED_PROVIDER_virtual/kernel ?= "${@bb.utils.contains('RT_KERNEL_AMD', 'yes', "linux-yocto-rt", "linux-yocto", d)}"
PREFERRED_VERSION_linux-yocto ?= "4.9%"
PREFERRED_VERSION_linux-yocto-rt ?= "4.9%"

require conf/machine/include/tune-snowyowl.inc

include conf/machine/include/amd-common-configurations.inc
include conf/machine/include/amd-customer-configurations.inc

MACHINE_FEATURES_remove = "alsa"
MACHINE_EXTRA_RRECOMMENDS_remove = "vdpauinfo rtc-test alsa-utils"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
           "

# Setup a getty on all serial ports
SERIAL_CONSOLES ?= "115200;ttyS4 115200;ttyS5"

# Enable the kernel console on ttyS4/COM0
KERNEL_SERIAL_CONSOLE ?= "console=ttyS4,115200n8"

# Disable the graphical console, no graphics support for now
# this allows the serial console to be used for installation
APPEND_remove="console=tty0"

# Drop features we do not need
DISTRO_FEATURES_remove="opengl x11 alsa pulseaudio"

MACHINEOVERRIDES =. "amd:amdx86:"

# Metadata used by CodeBench for the ADE
ADE_CB_CPU = "general.cpu.zen"
